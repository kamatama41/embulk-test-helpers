buildscript {
    ext.kotlin_version = '1.0.6'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '1.6.0'
    id 'net.researchgate.release' version '2.5.0'
}
apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task removeRepository {
    doLast {
        grgit.remove(patterns: ['repository'])
        grgit.commit(message: 'Remove repository')
    }
}

// Configuration for maven-publish plugin
publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.kamatama41'
            artifactId 'embulk-test-helpers'
            version version

            from components.java

            // Add source jar to publication
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file://${new File(rootDir, "repository").absolutePath}"
        }
    }
}

// Commit 'repository' dir after publishing by override publish task
publish {
    doLast {
        grgit.add(patterns: ['repository'])
        grgit.commit(message: 'Release new version')
    }
}

// Configuration for release plugin
release {
    git { requireBranch = 'gh-pages' }
}
afterReleaseBuild.dependsOn publish

dependencies {
    compile "org.embulk:embulk-standards:0.8.16"
    compile "org.embulk:embulk-test:0.8.16"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
